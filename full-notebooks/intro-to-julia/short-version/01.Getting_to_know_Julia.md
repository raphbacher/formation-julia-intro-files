
# Getting to know Julia


This notebook is meant to offer a crash course in Julia syntax to show you that Julia is lightweight and easy to use -- like your favorite high-level language!

We'll talk about
- Strings
- Data structures
- Loops
- Conditionals
- Functions

## Strings


<div class="input_area">
	
```julia
string1 = "How many cats "
```

</div>




    "How many cats "




<div class="input_area">
	
```julia
string2 = "is too many cats?"
```

</div>




    "is too many cats?"




<div class="input_area">
	
```julia
string(string1, string2)
```

</div>




    "How many cats is too many cats?"




<div class="input_area">
	
```julia
ðŸ˜º = 10
println("I don't know but $ðŸ˜º are too few!")
```

</div>

    I don't know but 10 are too few!


Note: Julia allows us to write super generic code, and ðŸ˜º is an example of this. 

This allows us to write code like


<div class="input_area">
	
```julia
ðŸ˜º = 1
ðŸ˜€ = 0
ðŸ˜ž = -1
```

</div>




    -1




<div class="input_area">
	
```julia
ðŸ˜º + ðŸ˜ž == ðŸ˜€
```

</div>




    true



## Data structures

### Tuples

We can create a tuple by enclosing an ordered collection of elements in `( )`.

Syntax: <br>
```julia
(item1, item2, ...)```


<div class="input_area">
	
```julia
myfavoriteanimals = ("penguins", "cats", "sugargliders")
```

</div>




    ("penguins", "cats", "sugargliders")




<div class="input_area">
	
```julia
myfavoriteanimals[1]
```

</div>




    "penguins"



### Dictionaries

If we have sets of data related to one another, we may choose to store that data in a dictionary. To do this, we use the `Dict()` function.

Syntax:
```julia
Dict(key1 => value1, key2 => value2, ...)```

A good example of a dictionary is a contacts list, where we associate names with phone numbers.


<div class="input_area">
	
```julia
myphonebook = Dict("Jenny" => "867-5309", "Ghostbusters" => "555-2368")
```

</div>




    Dict{String,String} with 2 entries:
      "Jenny"        => "867-5309"
      "Ghostbusters" => "555-2368"




<div class="input_area">
	
```julia
myphonebook["Jenny"]
```

</div>




    "867-5309"



### Arrays

Unlike tuples, arrays are mutable. Unlike dictionaries, arrays contain ordered sequences of elements. <br>
We can create an array by enclosing this sequence of elements in `[ ]`.

Syntax: <br>
```julia
[item1, item2, ...]```


For example, we might create an array to keep track of my friends


<div class="input_area">
	
```julia
myfriends = ["Ted", "Robyn", "Barney", "Lily", "Marshall"]
```

</div>




    5-element Array{String,1}:
     "Ted"     
     "Robyn"   
     "Barney"  
     "Lily"    
     "Marshall"




<div class="input_area">
	
```julia
fibonacci = [1, 1, 2, 3, 5, 8, 13]
```

</div>




    7-element Array{Int64,1}:
      1
      1
      2
      3
      5
      8
     13




<div class="input_area">
	
```julia
mixture = [1, 1, 2, 3, "Ted", "Robyn"]
```

</div>




    6-element Array{Any,1}:
     1       
     1       
     2       
     3       
      "Ted"  
      "Robyn"



We can also create arrays of other data structures, or multi-dimensional arrays.


<div class="input_area">
	
```julia
numbers = [[1, 2, 3], [4, 5], [6, 7, 8, 9]]
```

</div>




    3-element Array{Array{Int64,1},1}:
     [1, 2, 3]   
     [4, 5]      
     [6, 7, 8, 9]




<div class="input_area">
	
```julia
rand(4, 3)
```

</div>




    4Ã—3 Array{Float64,2}:
     0.41536   0.199777  0.224917
     0.650967  0.455228  0.586753
     0.811018  0.870951  0.879882
     0.745686  0.579426  0.813446



## Loops

### `for` loops

The syntax for a `for` loop is

```julia
for *var* in *loop iterable*
    *loop body*
end
```


<div class="input_area">
	
```julia
for n in 1:10
    println(n)
end
```

</div>

    1
    2
    3
    4
    5
    6
    7
    8
    9
    10


### `while` loops

The syntax for a `while` is

```julia
while *condition*
    *loop body*
end
```


<div class="input_area">
	
```julia
n = 0
while n < 10
    n += 1
    println(n)
end
```

</div>

    1
    2
    3
    4
    5
    6
    7
    8
    9
    10


## Conditionals

#### with `if`

In Julia, the syntax

```julia
if *condition 1*
    *option 1*
elseif *condition 2*
    *option 2*
else
    *option 3*
end
```

allows us to conditionally evaluate one of our options.


<div class="input_area">
	
```julia
x, y = 43,42# Enter two numbers here!
if x > y
    x
else
    y
end
```

</div>




    43



#### with ternary operators

For this last block, we could instead use the ternary operator with the syntax

```julia
a ? b : c
```

which equates to 

```julia
if a
    b
else
    c
end
```


<div class="input_area">
	
```julia
(x > y) ? x : y
```

</div>




    43



## Functions

Topics:
1. How to declare a function
2. Duck-typing in Julia
3. Mutating vs. non-mutating functions
4. Some higher order functions

### How to declare a function

#### First way: with `function` and `end` keywords


<div class="input_area">
	
```julia
function f(x)
    x^2
end
```

</div>




    f (generic function with 1 method)



#### Second way: with `=`


<div class="input_area">
	
```julia
f2(x) = x^2
```

</div>




    f2 (generic function with 1 method)



Third way: as an anonymous function


<div class="input_area">
	
```julia
f3 = x -> x^2
```

</div>




    #3 (generic function with 1 method)



#### Calling these functions


<div class="input_area">
	
```julia
f(42)
```

</div>




    1764




<div class="input_area">
	
```julia
f2(42)
```

</div>




    1764




<div class="input_area">
	
```julia
f3(42)
```

</div>




    1764



### Duck-typing in Julia
*"If it quacks like a duck, it's a duck."* <br><br>
Julia functions will just work on whatever inputs make sense. <br><br>
For example, `f` will work on a matrix. 


<div class="input_area">
	
```julia
A = rand(3, 3)
A
```

</div>




    3Ã—3 Array{Float64,2}:
     0.755473  0.327141  0.0776725
     0.901061  0.221114  0.947983 
     0.570018  0.871543  0.612919 




<div class="input_area">
	
```julia
f(A)
```

</div>




    3Ã—3 Array{Float64,2}:
     0.909788  0.387177  0.416411
     1.42033   1.16987   0.860637
     1.56532   0.913372  1.24615 



On the other hand, `f` will not work on a vector. Unlike `A^2`, which is well-defined, the meaning of `v^2` for a vector, `v`, is ambiguous. 


<div class="input_area">
	
```julia
v = rand(3)
```

</div>




    3-element Array{Float64,1}:
     0.045367062724664065
     0.04565883354805855 
     0.032975147466980825




<div class="input_area">
	
```julia
f(v)
```

</div>


    MethodError: no method matching ^(::Array{Float64,1}, ::Int64)
    Closest candidates are:
      ^(!Matched::Float16, ::Integer) at math.jl:795
      ^(!Matched::Missing, ::Integer) at missing.jl:120
      ^(!Matched::Missing, ::Number) at missing.jl:93
      ...

    

    Stacktrace:

     [1] macro expansion at ./none:0 [inlined]

     [2] literal_pow at ./none:0 [inlined]

     [3] f(::Array{Float64,1}) at ./In[20]:2

     [4] top-level scope at In[29]:1


### Mutating vs. non-mutating functions

By convention, functions followed by `!` alter their contents and functions lacking `!` do not.

For example, let's look at the difference between `sort` and `sort!`.


<div class="input_area">
	
```julia
v = [3, 5, 2]
```

</div>




    3-element Array{Int64,1}:
     3
     5
     2




<div class="input_area">
	
```julia
sort(v)
```

</div>




    3-element Array{Int64,1}:
     2
     3
     5




<div class="input_area">
	
```julia
v
```

</div>




    3-element Array{Int64,1}:
     3
     5
     2



`sort(v)` returns a sorted array that contains the same elements as `v`, but `v` is left unchanged. <br><br>

On the other hand, when we run `sort!(v)`, the contents of v are sorted within the array `v`.


<div class="input_area">
	
```julia
sort!(v)
```

</div>




    3-element Array{Int64,1}:
     2
     3
     5




<div class="input_area">
	
```julia
v
```

</div>




    3-element Array{Int64,1}:
     2
     3
     5



### Some higher order functions

#### map

`map` is a "higher-order" function in Julia that *takes a function* as one of its input arguments. 
`map` then applies that function to every element of the data structure you pass it. For example, executing

```julia
map(f, [1, 2, 3])
```
will give you an output array where the function `f` has been applied to all elements of `[1, 2, 3]`
```julia
[f(1), f(2), f(3)]
```


<div class="input_area">
	
```julia
map(f, [1, 2, 3])
```

</div>




    3-element Array{Int64,1}:
     1
     4
     9



Here we've squared all the elements of the vector `[1, 2, 3]`, rather than squaring the vector `[1, 2, 3]`.

To do this, we could have passed to `map` an anonymous function rather than a named function, such as


<div class="input_area">
	
```julia
x -> x^3
```

</div>




    #5 (generic function with 1 method)



via


<div class="input_area">
	
```julia
map(x -> x^3, [1, 2, 3])
```

</div>




    3-element Array{Int64,1}:
      1
      8
     27



and now we've cubed all the elements of `[1, 2, 3]`!

### broadcast

`broadcast` is another higher-order function like `map`. `broadcast` is a generalization of `map`, so it can do every thing `map` can do and more. The syntax for calling `broadcast` is the same as for calling `map`


<div class="input_area">
	
```julia
broadcast(f, [1, 2, 3])
```

</div>




    3-element Array{Int64,1}:
     1
     4
     9



and again, we've applied `f` (squared) to all the elements of `[1, 2, 3]` - this time by "broadcasting" `f`!

Some syntactic sugar for calling `broadcast` is to place a `.` between the name of the function you want to `broadcast` and its input arguments. For example,

```julia
broadcast(f, [1, 2, 3])
```
is the same as
```julia
f.([1, 2, 3])
```


<div class="input_area">
	
```julia
f.([1, 2, 3])
```

</div>




    3-element Array{Int64,1}:
     1
     4
     9



Notice again how different this is from calling 
```julia
f([1, 2, 3])
```
We can square every element of a vector, but we can't square a vector!

To drive home the point, let's look at the difference between

```julia
f(A)
```
and
```julia
f.(A)
```
for a matrix `A`:


<div class="input_area">
	
```julia
A = [i + 3*j for j in 0:2, i in 1:3]
```

</div>




    3Ã—3 Array{Int64,2}:
     1  2  3
     4  5  6
     7  8  9




<div class="input_area">
	
```julia
f(A)
```

</div>




    3Ã—3 Array{Int64,2}:
      30   36   42
      66   81   96
     102  126  150



As before we see that for a matrix, `A`,
```
f(A) = A^2 = A * A
``` 

On the other hand,


<div class="input_area">
	
```julia
B = f.(A)
```

</div>




    3Ã—3 Array{Int64,2}:
      1   4   9
     16  25  36
     49  64  81



contains the squares of all the entries of `A`.

This dot syntax for broadcasting allows us to write relatively complex compound elementwise expressions in a way that looks natural/closer to mathematical notation. For example, we can write


<div class="input_area">
	
```julia
C = A .+ 2 .* f.(A) ./ A
```

</div>




    3Ã—3 Array{Float64,2}:
      3.0   6.0   9.0
     12.0  15.0  18.0
     21.0  24.0  27.0



instead of


<div class="input_area">
	
```julia
broadcast(x -> x + 2 * f(x) / x, A)
```

</div>




    3Ã—3 Array{Float64,2}:
      3.0   6.0   9.0
     12.0  15.0  18.0
     21.0  24.0  27.0



and this will still compile down to code that runs as efficiently as `C`!
